# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>
#Credits: Reza Jahanbakhshi <reza.jahanbakhshi at gmail dot com

pkgname=mesa-git
pkgdesc='An open-source implementation of the OpenGL specification. (git release)'
pkgver=22.2.0_devel.153761.e693e5e5950
pkgrel=1
commit=81557a4fdf1e780bef75ca4ba115395ec61aae18
arch=(x86_64)
url='https://mesa3d.org/'
license=(MIT)
makedepends=(meson ninja bison flex python python-mako valgrind cmake
             xorgproto wayland-protocols glslang libclc elfutils libarchive libconfig compiler-rt libxcb
             wayland libxml2 libx11 libvdpau libelf libglvnd ncurses libva libxrandr expat libdrm
             libxv libxshmfence libxxf86vm libxdamage zstd lm_sensors libunwind vulkan-icd-loader
             llvm clang llvm-libs glibc gcc gcc-libs libomxil-bellagio directx-headers)
depends=(libdrm libxcb wayland python libclc libx11 libxshmfence zstd libelf expat libxxf86vm
         libxdamage libomxil-bellagio libunwind lm_sensors libglvnd vulkan-icd-loader
         llvm-libs gcc-libs glslang clang)
#depends+=(libsensors.so libexpat.so libvulkan.so)
conflicts=(mesa opencl-mesa vulkan-intel vulkan-radeon vulkan-mesa vulkan-swrast vulkan-mesa-layers libva-mesa-driver mesa-vdpau)
provides=(mesa opencl-mesa vulkan-intel vulkan-radeon vulkan-swrast vulkan-driver vulkan-mesa-layers libva-mesa-driver mesa-vdpau
          opengl-driver opencl-driver mesa-libgl)
source=(git+https://gitlab.freedesktop.org/mesa/mesa.git#commit=$commit)
md5sums=(SKIP)

pkgver(){
  cd ${srcdir}/mesa
  read -r _ver <VERSION
  echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
  cd ${srcdir}/mesa

  # Apply any patch
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build(){
  cd ${srcdir}/mesa

  rm -rf -v build_64

  mkdir -p -v build_64

  meson setup build_64/ \
  -D b_ndebug=true \
  -D buildtype=plain \
  --wrap-mode=nofallback \
  -Dprefix=/usr \
  -D sysconfdir=/etc \
  -Dplatforms=x11,wayland \
  -Degl-native-platform=auto \
  -Ddri3=enabled \
  -Dgallium-drivers=r300,r600,radeonsi,nouveau,virgl,svga,swrast,iris,zink,crocus,d3d12 \
  -Dgallium-extra-hud=true \
  -Dgallium-vdpau=enabled \
  -Dgallium-xvmc=disabled \
  -Dgallium-omx=bellagio \
  -Dgallium-va=enabled \
  -Dgallium-xa=enabled \
  -Dgallium-nine=true \
  -Dgallium-d3d10umd=false \
  -Dgallium-opencl=icd \
  -Dopencl-spirv=false \
  -Dopencl-native=true \
  -Dvulkan-drivers=amd,intel,swrast,virtio-experimental,imagination-experimental \
  -Dimagination-srv=true \
  -Dshader-cache=enabled \
  -Dvulkan-layers=device-select,intel-nullhw,overlay \
  -Dshared-glapi=enabled \
  -Dgles1=enabled \
  -Dgles2=enabled \
  -Dopengl=true \
  -Dgbm=enabled \
  -Dglx=dri \
  -Degl=enabled \
  -Dglvnd=true \
  -Dmicrosoft-clc=disabled \
  -Dspirv-to-dxil=false \
  -Dllvm=enabled \
  -Dshared-llvm=enabled \
  -Dvalgrind=enabled \
  -Dlibunwind=enabled \
  -Dlmsensors=enabled \
  -Dselinux=false \
  -Dosmesa=true \
  -Dxlib-lease=enabled \
  -Dglx-direct=true \
  -Dzstd=enabled \
  -Dzlib=enabled \
  -Dsse2=false \
  -Dvulkan-beta=true \
  -Dintel-clc=enabled \
  -Dvideo-codecs=vc1dec,h264dec,h264enc,h265dec,h265enc \
  -Dgallium-d3d12-video=enabled

  meson configure build_64/

  meson compile -C build_64/

  ninja -C build_64/
}

package_mesa-git(){
  # compiling mesa and installing to "$pkgdir"
  DESTDIR="${pkgdir}" ninja -C ${srcdir}/mesa/build_64/ install

  # remove script file from /usr/bin
  rm "${pkgdir}/usr/bin/mesa-overlay-control.py"
  rm -rf -v "${pkgdir}/usr/bin"

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir}/usr/lib/libGLX_indirect.so.0"

  # installing license
  #install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 LICENSE
  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
